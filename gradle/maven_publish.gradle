/*
 * Copyright 2021 Kevin zhou
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'maven-publish'
apply plugin: 'signing'

// load properties
Properties properties = new Properties()
File localPropertiesFile = rootProject.file("local.properties");
if (localPropertiesFile.exists()) {
    properties.load(localPropertiesFile.newDataInputStream())
}

def developerId = properties.getProperty("POM_DEVELOPER_ID")
def developerName = properties.getProperty("POM_DEVELOPER_NAME")
def developerEmail = properties.getProperty("POM_DEVELOPER_EMAIL")

def ossrhUser = properties.getProperty("OSSRH_USER")
def ossrhPswd = properties.getProperty("OSSRH_PSWD")

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = GROUP_ID
            artifactId = POM_ARTIFACT_ID
            version = VERSION_NAME

            pom {
                name = PROJECT_NAME
                description = POM_DESCRIPTION
                url = PROJECT_URL
                developers {
                    developer {
                        id = developerId
                        name = developerName
                        email = developerEmail
                    }
                }
                scm {
                    connection = GIT_URL
                    developerConnection = WEB_URL
                    url = PROJECT_URL
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            allowInsecureProtocol true
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUser
                password = ossrhPswd
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
